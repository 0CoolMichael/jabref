// The purpose of this package is to generate a fat jar for the Apollo client library in order to solve split package issues.
// This is a ugly workaround for https://github.com/apollographql/apollo-android/issues/3152.

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation ("com.apollographql.apollo:apollo-runtime:2.5.9"){
        exclude module: "apollo-api"
        exclude module: "apollo-normalized-cache"
    }
    implementation 'com.apollographql.apollo:apollo-api-jvm:2.5.9'

}

shadowJar {
    // remove this one as it comes from the kotlin stdlib
    exclude '**/module-info.class'
    mergeServiceFiles()
    archiveBaseName.set("apollo")
    // we need to rename these packages to avoid split package
    relocate 'org.jetbrains.annotations', 'shadow.jetbrains.annotations'
    relocate 'org.intellij.lang.annotations', 'shadow.intellij.lang.annotations'
}

jar {
   manifest {
       attributes 'Automatic-Module-Name': 'com.customapollo.thirdparty.apollo'
   }
}
